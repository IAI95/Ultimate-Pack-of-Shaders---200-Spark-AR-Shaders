precision highp float;

float rand(vec2 p)
{
    float iTime = std::getTime();
    float t = floor(iTime * 20.) / 10.;
    return fract(sin(dot(p, vec2(t * 12.9898, t * 78.233))) * 43758.5453);
}

float noise(vec2 uv, float blockiness)
{   
    vec2 lv = fract(uv);
    vec2 id = floor(uv);
    
    float n1 = rand(id);
    float n2 = rand(id+vec2(1,0));
    float n3 = rand(id+vec2(0,1));
    float n4 = rand(id+vec2(1,1));
    
    vec2 u = smoothstep(0.0, 1.0 + blockiness, lv);

    return mix(mix(n1, n2, u.x), mix(n3, n4, u.x), u.y);
}

float fbm(vec2 uv, int count, float blockiness, float complexity)
{
    float val = 0.0;
    float amp = 0.5;
    
    while(count != 0)
    {
    	val += amp * noise(uv, blockiness);
        amp *= 0.5;
        uv *= complexity;    
        count--;
    }
    
    return val;
}

// @param[default=0.1, min=0.0, max=1.0] GlitchAmplitude
// @param[default=4.0, min=0.0, max=10.0] GlitchNarrowness
// @param[default=2.0, min=0.0, max=10.0] GlitchBlockiness
// @param[default=5.0, min=0.0, max=10.0] GlitchMinimizer

void mainImage( in float GlitchAmplitude, in float GlitchNarrowness, in float GlitchBlockiness, in float GlitchMinimizer, in std::Texture2d Texture, out vec4 Return )
{

    vec2 iResolution = std::getRenderTargetSize();
    vec2 fragCoord = fragment(floor(std::getRenderTargetSize() * std::getVertexTexCoord()));
    float iTime = std::getTime();

    // Normalized pixel coordinates (from 0 to 1)
    vec2 uv = fragCoord/iResolution.xy;
    vec2 a = vec2(uv.x * (iResolution.x / iResolution.y), uv.y);
    vec2 uv2 = vec2(a.x / iResolution.x, exp(a.y));
	vec2 id = floor(uv * 8.0);
    //id.x /= floor(Texture.sample(vec2(id / 8.0)).r * 8.0);

    // Generate shift amplitude
    float shift = GlitchAmplitude * pow(fbm(uv2, int(rand(id) * 6.), GlitchBlockiness, GlitchNarrowness), GlitchMinimizer);
    
    // Create a scanline effect
    float scanline = abs(cos(uv.y * 400.));
    scanline = smoothstep(0.0, 2.0, scanline);
    shift = smoothstep(0.00001, 0.2, shift);
    
    // Apply glitch and RGB shift
    float colR = Texture.sample(vec2(uv.x + shift, uv.y)).r * (1. - shift) ;
    float colG = Texture.sample(vec2(uv.x - shift, uv.y)).g * (1. - shift) + rand(id) * shift;
    float colB = Texture.sample(vec2(uv.x - shift, uv.y)).b * (1. - shift);
    // Mix with the scanline effect
    vec3 f = vec3(colR, colG, colB) - (0.1 * scanline);
    
    // Output to screen
    Return = vec4(f, 1.0);
}